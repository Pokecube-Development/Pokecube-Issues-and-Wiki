buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        flatDir {
          dirs 'libs'
        }
        maven {url='https://repo.spongepowered.org/repository/maven-public/'}
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
  id 'org.ajoberstar.grgit' version '2.3.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

eclipseJdtApt.enabled = false
eclipseFactoryPath.enabled = false

mixin { add sourceSets.main, "pokecube.refmap.json" }

ext {
    VERSION_COMPILE = gitVersion("${version_major}.${version_minor}")
    VERSION_THUTCORE = gitVersion("${version_thutcore}.${version_major}.${version_minor}")
    // Force a refresh of the toml file
    cleanTOML()
}

version = "${version_minecraft}-${VERSION_COMPILE}"
group = 'thut'
archivesBaseName = "${compile_name}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: "${channel_mappings}", version: "${version_mappings}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client = {
            taskName 'run_client_1'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            properties '-XX': '+UnlockCommercialFeatures'
            properties '-XX': '+FlightRecorder'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            arg "-mixin.config=pokecube.mixins.json"
            source sourceSets.main
        }
        client_2 {
            parent runs.client
            taskName 'run_client_2'

            environment 'MOD_CLASSES', 'dummy' // Needed to work around FG limitation, FG will replace this!
            args '-mixin.config=pokecube.mixins.json', '--username', 'Dev2'
        }
        test_client {
            parent runs.client
            taskName 'run_test_client'

            environment 'MOD_CLASSES', 'dummy' // Needed to work around FG limitation, FG will replace this!
            arg "-mixin.config=pokecube.mixins.json"

            mods {
                TestMods { sources sourceSets.test }
                MainMods { sources sourceSets.main }
            }
        }
        server = {
            taskName 'run_server'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            properties '-XX': '+UnlockCommercialFeatures'
            properties '-XX': '+FlightRecorder'
            jvmArg "-Xmx2G"
            workingDirectory project.file('run').canonicalPath
            arg "-mixin.config=pokecube.mixins.json"
            source sourceSets.main
        }
        test_server {
            parent runs.server
            taskName 'run_test_server'

            environment 'MOD_CLASSES', 'dummy' // Needed to work around FG limitation, FG will replace this!
            arg "-mixin.config=pokecube.mixins.json"

            mods {
                TestMods { sources sourceSets.test }
                MainMods { sources sourceSets.main }
            }
        }
        data = {
            workingDirectory project.file('run')
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'pokecube', '--all', '--output', file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]
            mods {
                pokecube {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    flatDir {
        dirs 'libs'
    }
    maven { // Computer Craft
        url 'https://squiddev.cc/maven/' 
    }
    //TODO check dependency
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "https://maven.tehnut.info"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
      }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven { // Minecolonies
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${version_minecraft}-${version_forge}"
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
    
    //Computer Craft Support.
    implementation fg.deobf("org.squiddev:cc-tweaked-${version_cct_mc}:${version_cct}")
    
    //Lost Cities
    implementation "blank:lostcities-1.16-4.0.7-beta"
    
    //Thut Essentials
    implementation "blank:thutessentials-1.16.5-4.1.5-deobf"
    
    //JEI
    // compile against the JEI API but do not include it at runtime
    compileOnly fg.deobf("mezz.jei:jei-${version_jei_mc}:${version_jei}:api")
    // at runtime, use the full JEI jar
    runtimeOnly fg.deobf("mezz.jei:jei-${version_jei_mc}:${version_jei}")
    
    //WHYLA
    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${version_hwyla}:api")
    runtimeOnly fg.deobf("mcp.mobius.waila:Hwyla:${version_hwyla}")
    
    //Minecolonies
    implementation "blank:minecolonies-0.13.437-RELEASE-api"
    
    //Immersive Enginnering
    //implementation "blusunrize:ImmersiveEngineering:0.15-+"
    
    // Advanced Rocketry
    implementation "blank:AdvancedRocketry-1.16.5-1.7.1-4-api"
    implementation "blank:LibVulpes-1.16.5-1.0.1-6-api"

    // Curios - Wearables Support
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${version_curios}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${version_curios}:api")
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

jar {
    manifest {
        attributes 'FMLAT': 'accesstransformer.cfg',
        "Specification-Title": "${compile_name}",
        "Specification-Vendor": "${author_name}",
        "Specification-Version": VERSION_COMPILE,
        "Implementation-Title": project.name,
        "Implementation-Version": VERSION_COMPILE,
        "Implementation-Vendor" :"${author_name}",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "MixinConfigs": "pokecube.mixins.json"
    }
}

processResources.doLast {
    def TOMLFILE = "./build/resources/main/META-INF/mods.toml"
    replaceParameters(TOMLFILE, "version_thutcore", VERSION_THUTCORE)
    replaceParameters(TOMLFILE, "version_mc", "${version_mcsupport}")
}

def replaceParameters(filename, find, replace) {
    ant.replace(file:filename,token:find,value:replace)
}

task sourcesJar(type: Jar) {
	classifier = "sources"
	from sourceSets.main.allJava
}

task serverJar(type: Jar) {	
    classifier = 'server'
    from sourceSets.main.output	
    manifest {
        attributes 'FMLAT': 'accesstransformer.cfg',
        "Specification-Title": "${compile_name}",
        "Specification-Vendor": "${author_name}",
        "Specification-Version": VERSION_COMPILE,
        "Implementation-Title": project.name,
        "Implementation-Version": VERSION_COMPILE,
        "Implementation-Vendor" :"${author_name}",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "MixinConfigs": "pokecube.mixins.json"
    }
    exclude '**/assets/pokecube_mobs/gen*'
    exclude '**/assets/pokecube_mobs/sound*'
    exclude '**/assets/pokecube/sound*'
    exclude '**/textures*'
    exclude '**/models*'
    exclude '**/client*'
    exclude '**/blockstates*'
}

task deobfJar(type: Jar) {	
    classifier = 'deobf'
    from sourceSets.main.output	
}

artifacts {
	archives sourcesJar
    archives deobfJar
    reobf { serverJar {}}
}

def cleanTOML()
{
    delete "./build/resources/main/META-INF/mods.toml" 
}

def gitVersion(value) {
    if("${pre_release}".toBoolean())
    {
        return "${value}.0.d"
    }
    def raw = grgit.describe(longDescr: true)
    def desc = (raw == null ? 'unknown-offset-hash' : grgit.describe(longDescr: true)).split('-') as List
    def vers = desc.get(1)
    def splis = vers.split(/[.]/)
    def majver = splis[0].toInteger()
    def subver = splis[1].toInteger()
    def offset = splis[2].toInteger()
    def ver = offset + 1
    if (subver < version_minor.toInteger() || majver < version_major.toInteger()){
        ver = 0
    }
    return "${value}.${ver}"
}